plugins {
    id("dev.kikugie.stonecutter")
    id("net.neoforged.moddev") version "2.0.147" apply false
    id("net.neoforged.moddev.legacyforge") version "2.0.147" apply false
    id("me.modmuss50.mod-publish-plugin") version "2.2.0" apply false
    id("org.moddedmc.wiki.toolkit") version "0.4.1"
}

stonecutter active "1.21.1-fabric"

stonecutter parameters {
    val loader = node.metadata.project.substringAfterLast('-')
    constants.match(loader, "fabric", "forge", "neoforge")
    constants["forgelike"] = loader != "fabric"

    replacements {
        // Mojang renamed ResourceLocation in 26.1; this also covers our own `getResourceLocationFor*` helpers.
        // The reverse pattern must leave our ResourceIdentifier/IdentifierQualifier classes alone.
        regex(eval(node.metadata.version, ">=26.1")) {
            replace(
                "ResourceLocation", "Identifier",
                "(?<![A-Za-z])Identifier(?!Qualifier)|(?<=get)Identifier", "ResourceLocation"
            )
        }
    }
}

stonecutter tasks {
    order("publishModrinth")
    order("publishCurseforge")
}

for (version in stonecutter.versions.map { it.version }.distinct()) tasks.register("publish$version") {
    group = "publishing"
    dependsOn(stonecutter.tasks.named("publishMods") { metadata.version == version })
}

wiki {
    wikiAccessToken = System.getenv("WIKI_ACCESS_TOKEN")
    docs.create("treeliable") {
        root = file("docs/")
    }
}
